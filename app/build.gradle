apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "23.0.0"

    defaultConfig {
        applicationId "com.elab.learner"
        minSdkVersion 19
        targetSdkVersion 21
    }

    sourceSets.main {
        //Tell Gradle where to put the compiled library
        jniLibs.srcDir 'src/main/libs'

        //hide the ‘jni’ folder so that the automatic gradle build doesn’t try to run
        //it’s own ndk-build process
        jni.srcDirs = [];
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    // Tell Gradle the run the ndkBuild task when compiling
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

    // This task utilizes the Android.mk file defined in src/main/jni so that you
    // have more control over the build parameters (like library inclusion)
    task ndkBuild(type: Exec) {
        def ndkDir = android.ndkDirectory.getAbsolutePath()
        commandLine ndkDir + '/ndk-build', '-C', file('src/main/jni').absolutePath
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.loopj.android:android-async-http:1.4.8'
}
